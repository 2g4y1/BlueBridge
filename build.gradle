plugins{
    id "java"
    id "idea"
}

subprojects {

    apply plugin: 'java'

    version '2.2'

    //Add maintainers here (separated by a comma and space). This will be added to the plugin.yml.
    def authors = 'Mark_225'

    sourceCompatibility = 21
    targetCompatibility = 21

    group 'de.mark225'

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
        maven {
            name = "papermc"
            url = uri("https://repo.papermc.io/repository/maven-public/")
        }
        maven {
            name = "bluemap-releases"
            url = uri("https://repo.bluecolored.de/releases")
        }
        maven {
            name = "bluemap-snapshots"
            url = uri("https://repo.bluecolored.de/snapshots")
        }
    }

    configurations {
        provided
        implementation.extendsFrom provided
    }

    dependencies {
        compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
        provided 'com.flowpowered:flow-math:1.0.3'
    }

    tasks.withType(Jar) {
        destinationDirectory = file("$rootDir/build/target")
    }

    processResources {
        filesMatching('plugin.yml') {
            expand([gradleVersion: version, contributors: authors])
        }
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}